@@grammar::Calc
@@parseinfo::False

start
    =
    expression $
    ;


expression
    =
    | addition
    | subtraction
    | term
    ;


addition
    =
    left:term op:'+' ~ right:expression
    ;


subtraction
    =
    left:term op:'-' ~ right:expression
    ;


term
    =
    | multiplication
    | division
    | unary
    ;


multiplication
    =
    left:unary op:'*' ~ right:term
    ;


division
    =
    left:unary op:'/' ~ right:term
    ;

unop
    =
    op:('*' | '-' | '+' | '~' | '!' | '--' | '++') right:unary
    ;

postop
    =
    | left:post_wrap op:('++' | '--') type:`a`
    | left:post_wrap '[' ~ op:expression ']' type:`b`
    ;

post_wrap
  =
  | factor
  | postop
  ;

unary
  =
  | post_wrap
  | unop
  ;

factor
    =
    | number
    | subexpression
    ;


subexpression
    =
    '(' @:expression ')'
    ;


number
    =
    /\d+/
    ;
